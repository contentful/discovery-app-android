apply plugin: 'com.android.application'

android {
  compileSdkVersion 23
  buildToolsVersion '23.0.0'

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 23
    applicationId "com.contentful.discovery"
    versionCode 6
    versionName "1.0.1"
  }

  signingConfigs {
    release {}
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }

  packagingOptions {
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
    exclude 'LICENSE.txt'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  lintOptions {
    // For Okio
    // https://github.com/square/okio/issues/58
    warning 'InvalidPackage'
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  // remote
  compile 'com.android.support:support-v4:23.0.0'
  compile 'com.google.android.gms:play-services-gcm:7.8.0'
  compile 'com.google.android.gms:play-services-maps:7.8.0'
  compile 'com.jakewharton:butterknife:5.1.2'
  compile 'com.squareup.picasso:picasso:2.5.2'
  compile 'org.apache.commons:commons-lang3:3.3.2'
  compile 'commons-io:commons-io:2.4'
  compile 'org.markdownj:markdownj-core:0.4'
  compile 'joda-time:joda-time:2.4'
  compile 'com.contentful.java:java-sdk:4.0.2'
  compile 'com.viewpagerindicator:library:2.4.1@aar'
}

// Sign properties
File f = file(project.file("sign.properties"))

if (f.exists()) {
  Properties props = new Properties()
  props.load(new FileInputStream(file(project.file("sign.properties"))))

  android.signingConfigs.release.storeFile = file((String) props["key.store"].toURI())
  android.signingConfigs.release.keyAlias = props["key.alias"]
  android.signingConfigs.release.storePassword = props["key.store.password"]
  android.signingConfigs.release.keyPassword = props["key.alias.password"]
}

apply from: rootProject.file('gradle/checkstyle.gradle')